%%% ## Author: Syed Faraz Abrar
%%% ## Twitter: https://twitter.com/farazsth98
%%% ## Description: Grammar for fuzzing JavaScript

%const% MAX_REPEAT_POWER := 2
%const% VARIANCE_MIN := 50
%const% VARIANCE_MAX := 100
%const% VARIABLE_MIN := 1
%const% VARIABLE_MAX := 2

%%% ###########################################################################
%%% ###########################################################################
%%% ####### VALUE SECTION
%%% ####### Only define values here
%%% ####### Use and assign value in declarations as follows: +value+
%%% ####### Use variables in statements as follows: !variable!.bar();
%%% ###########################################################################
%%% ###########################################################################

%section% := value

%%% ######## Generic Values ########

integers :=
	%range%(-64-64)

unsigned := 
	%range%(0-64)

floats :=
	%range%(-64.0-64.0)

strings :=
	%range%(a-z)
	%range%(A-Z)
	%range%(0-9)
	%repeat%(+strings+)

booleans :=
	true
	false

others :=
	+integers+
	+floats+
	"+strings+"
	+booleans+
	{}
	[]
	{'a':+integers+}
	!str!
	!obj!
	!arr!
	NaN
	undefined
	null

unsafeSizes :=
	%range%(0-64)
	255
	256
	127
	128

charCodes :=
	0x%range%(0x0-0xff)
	+charCodes+, +charCodes+

twoIntArgs :=
	%range%(0-64), %range%(0-64)

threeIntArgs :=
	%range%(0-64), %range%(0-64), %range%(0-64)
	
twoFloatArgs :=
	%range%(0.0-64.0), %range%(0.0-64.0)

threeFloatArgs :=
	%range%(0.0-64.0), %range%(0.0-64.0), %range%(0.0-64.0)

%%% ##############################################################################################
%%% ##############################################################################################
%%% ######## Generic Functions ########
%%% ##############################################################################################
%%% ##############################################################################################

genericFuncArrayMap :=
	function(c_val, c_index, c_array) { c_array.+JSArrayInPlaceMethod+ }

genericFuncArraySort :=
	function(c_first, c_second) { c_first < c_second; }
	function(c_first, c_second) { c_first <= c_second; }
	function(c_first, c_second) { c_first >= c_second; }
	function(c_first, c_second) { c_first > c_second; }

%%% ##############################################################################################
%%% ##############################################################################################
%%% ######## JSArrays ########
%%% ## https://developer.mozilla.org/en-US/docs/Web/JavaScript/Reference/Global_Objects/Array ##
%%% ##############################################################################################
%%% ##############################################################################################

arrayMethods :=
	+JSArrayCallbackMethod+
	+JSArrayInPlaceMethod+
	+JSArrayForLoopUnsafe+

arrayMethodsForLoop :=
	+JSArrayCallbackMethod+
	+JSArrayInPlaceMethod+

%%% ######## Methods with callbacks ########

JSArrayCallbackMethod :=
%%%	+every+
%%%	+filter+
%%%	+find+
%%%	+findIndex+
%%%	+flatMap+
%%%	+forEach+
	+map+
%%%	+reduce+
%%%	+reduceRight+
%%%	+some+
	+sort+

map :=
	map(+genericFuncArrayMap+)

sort :=
	sort()
	sort(+genericFuncArraySort+)
	
%%% ######## Methods that occur in-place ########

JSArrayForLoopUnsafe :=
	+push+
	+splice+
	+unshift+

JSArrayInPlaceMethod :=
	+concat+
	+copyWithin+
%%%	+entries+
	+fill+
	+flat+
	+includes+
	+indexOf+
	+join+
%%%	+keys+
	+lastIndexOf+
	+pop+
	+reverse+
	+shift+
	+slice+
%%%	+toLocaleString+
	+toString+
%%%	+values+

concat :=
	concat()
	concat(!arr!)
	concat(!obj!)

copyWithin :=
	copyWithin(+twoIntArgs+)
	copyWithin(+threeIntArgs+)

flat :=
	flat()
	flat(+others+)

%%%	entries :=

fill :=
	fill(+others+)
	fill(+others+, +twoIntArgs+)

includes :=
	includes(+others+)
	includes(+others+, +unsigned+)

indexOf :=
	indexOf(+others+)
	indexOf(+others+, +unsigned+)

join :=
	join()
	join(+others+)

%%%	keys :=

lastIndexOf :=
	lastIndexOf(+others+)
	lastIndexOf(+others+, +unsigned+)

pop :=
	pop()
	pop(%repeat%(+others+, ", "))

push :=
	push()
	push(%repeat%(+others+, ", "))

reverse :=
	reverse()
	reverse(%repeat%(+others+, ", "))

shift :=
	shift()
	shift(%repeat%(+others+, ", "))

slice :=
	slice()
	slice(+unsigned+)
	slice(+unsigned+, +unsigned+)

splice :=
	splice()
	splice(+unsigned+)
	splice(+unsigned+, +unsigned+)
	splice(+unsigned+, +unsigned+, %repeat%(+others+, ", "))

toString :=
	toString()
	toString(%repeat%(+others+, ", "))

unshift :=
	unshift()
	unshift(%repeat%(+others+, ", "))
	
%%% ##############################################################################################
%%% ##############################################################################################
%%% ######## DataViews ########
%%% ### https://developer.mozilla.org/en-US/docs/Web/JavaScript/Reference/Global_Objects/DataView
%%% ##############################################################################################
%%% ##############################################################################################

DataViewGetMethods :=
    getFloat32
    getFloat64
    getInt16
    getInt32
    getInt8
    getUint16
    getUint32
    getUint8

DataViewSetMethods :=
    setFloat32
    setFloat64
    setInt16
    setInt32
    setInt8
    setUint16
    setUint32
    setUint8

DataViewMethods :=
	+DataViewGetMethods+(%range%(0-4), +booleans+)
	+DataViewSetMethods+(%range%(0-4), +integers+)
	+DataViewSetMethods+(%range%(0-4), +floats+)
	+DataViewSetMethods+(%range%(0-4), +others+)

%%% ##############################################################################################
%%% ##############################################################################################
%%% ######## Wrappers ########
%%% ##############################################################################################
%%% ##############################################################################################

forLoopInit :=
	for (var i = 0; i < !e_arr!.length; i++) { !e_arr![i] = +others+; }
	for (var i = 0; i < !t_arr!.length; i++) { !t_arr![i] = +integers+; }
	for (var i = 0; i < !t_arr!.length; i++) { !t_arr![i] = +floats+; }
	for (var i = 0; i < !t_arr!.length; i++) { !t_arr![i] = +others+; }

forLoops :=
	for (var i = 0; i < +unsigned+; i++) { !arr!.+arrayMethods+; }
	for (var i = 0; i < !arr!.length; i++) { !arr!.+arrayMethodsForLoop+; }
	for (var i = 0; i < +unsigned+; i++) { !e_arr!.+arrayMethods+; }
	for (var i = 0; i < !arr!.length; i++) { !e_arr!.+arrayMethodsForLoop+; }
	+forLoopInit+

wrapper :=
	!arr!.+arrayMethods+;
	!e_arr!.+arrayMethods+;
	!t_arr!.+arrayMethods+;
	!dv!.+DataViewMethods+;

%%% ###########################################################################
%%% ###########################################################################
%%% ####### VARIABLE SECTION
%%% ####### Only assign variables in this section
%%% ####### For example: @var@ = 42;
%%% ###########################################################################
%%% ###########################################################################

%section% := variable

str :=
	try { var @str@ = "+strings+"; } catch (e) {}
	try { var @str@ = String.fromCharCode(+charCodes+); } catch (e) {}

obj :=
	try { var @obj@ = {}; } catch (e) {}
	try { var @obj@ = Object.create(null); } catch (e) {}
	try { var @obj@ = Object.create(!obj!); } catch (e) {}

arr :=
	try { var @arr@ = new Array(%repeat%(+floats+, ", ")); } catch (e) {}
	try { var @arr@ = new Array(%repeat%(+others+, ", ")); } catch (e) {}
	try { var @arr@ = Array.of(%repeat%(+unsigned+, ", ")); } catch (e) {}
	try { var @arr@ = Array.of(%repeat%(+floats+, ", ")); } catch (e) {}
	try { var @arr@ = Array.of(%repeat%(+others+, ", ")); } catch (e) {}

e_arr :=
	try { var @e_arr@ = new Array(+unsafeSizes+); } catch (e) {}

t_arr :=
	try { var @t_arr@ = new Int8Array(+unsafeSizes+); } catch (e) {}
	try { var @t_arr@ = new Uint8Array(+unsafeSizes+); } catch (e) {}
	try { var @t_arr@ = new Uint8ClampedArray(+unsafeSizes+); } catch (e) {}
	try { var @t_arr@ = new Int16Array(+unsafeSizes+); } catch (e) {}
	try { var @t_arr@ = new Uint16Array(+unsafeSizes+); } catch (e) {}
	try { var @t_arr@ = new Int32Array(+unsafeSizes+); } catch (e) {}
	try { var @t_arr@ = new Uint32Array(+unsafeSizes+); } catch (e) {}
	try { var @t_arr@ = new Float32Array(+unsafeSizes+); } catch (e) {}
	try { var @t_arr@ = new Float64Array(+unsafeSizes+); } catch (e) {}
	try { var @t_arr@ = new Int8Array(!buf!); } catch (e) {}
	try { var @t_arr@ = new Uint8Array(!buf!); } catch (e) {}
	try { var @t_arr@ = new Uint8ClampedArray(!buf!); } catch (e) {}
	try { var @t_arr@ = new Int16Array(!buf!); } catch (e) {}
	try { var @t_arr@ = new Uint16Array(!buf!); } catch (e) {}
	try { var @t_arr@ = new Int32Array(!buf!); } catch (e) {}
	try { var @t_arr@ = new Uint32Array(!buf!); } catch (e) {}
	try { var @t_arr@ = new Float32Array(!buf!); } catch (e) {}
	try { var @t_arr@ = new Float64Array(!buf!); } catch (e) {}

buf :=
	try { var @buf@ = new ArrayBuffer(+unsafeSizes+); } catch (e) {}

dv :=
	try { var @dv@ = new DataView(!buf!); } catch (e) {}

%%% ###########################################################################
%%% ###########################################################################
%%% ####### VARIANCE SECTION
%%% ###########################################################################
%%% ###########################################################################

%section% := variance

main :=
	try { +forLoops+ } catch(e) {}
	try { +wrapper+ } catch(e) {}
